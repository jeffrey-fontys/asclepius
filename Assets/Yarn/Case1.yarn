title: Case1
---
<<if $PatientIntro is true>>
    <<jump Treatment>>
<<endif>>
<<set_dialogue_total "XR Origin" 2>>
Patient: Hallo.
-> Player: Goededag, hoe gaat het met u?
Patient: Kan beter. Ik heb veel last van deze wond hier.
-> Player: Oei, ik zie het. Dat ziet er niet best uit.
    <<add_dialogue_points "XR Origin" 1>>
-> Player: Niet zo aanstellen hè. Zo erg is het niet.
<<set $PatientIntro to true>>
<<jump Treatment>>
===
title: Treatment
---
Patient: Wat gaat er nu gebeuren?
-> Player: Ik zal de wond eerst even moeten spoelen. <<if !$WoundRinsed>>
    <<jump Rinse>>
-> Player: Ik ga de wond desinfecteren. <<if !$WoundDisinfected && $HeldItem is "Jodium">>
    <<jump Disinfect>>
-> Player: Ik ga uw wond verbinden met een verband. <<if !$WoundBandaged && $HeldItem is "Verband">>
    <<jump Bandaging>>
-> Player: Ik moet even iets pakken. Moment.
    Patient: Oké.
-> Player: Dat was het, u kunt gaan!
    Patient: Erg bedankt! Tot ziens!
    <<show_performance_review StateManager>>
===
title: Rinse
---
-> Player: Dit kan een beetje pijn doen.
Patient: ...
-> Player: Zo, uw wond is nu schoon.
<<set $WoundRinsed to true>>
<<jump Treatment>>
===
title: Disinfect
---
-> Player: Ik breng nu wat jodium aan. Dat kan een beetje prikken.
Patient: ...
-> Player: Zo, uw wond is nu gedesinfecteerd.
<<set $WoundDisinfected to true>>
<<use_held_item "XR Origin">>
<<jump Treatment>>
===
title: Bandaging
---
-> Player: Ik wikkel het verband nu om de wond.
Patient: ...
-> Player: Zit het niet te strak?
    Patient: Nee, het is prima zo.
    <<add_dialogue_points "XR Origin" 1>>
-> Player: Zo, het verband zit erom.
<<set $WoundBandaged to true>>
<<use_held_item "XR Origin">>
<<jump Treatment>>
===
